new;

// Results saving paths
root = "C:/Users/eclow/Documents/GitHub/cab-persistency/results/";
spath = root $+ "data_plots/reg_data/";
spath2 = root $+ "data_stats";

// Load data
reg_data = loadd("data/reg_data.gdat");

// Filter data to pre-covid period
post_data = selif(reg_data, reg_data[., "date2"] .>= "2020-03");
pre_data = selif(reg_data, reg_data[., "date2"] .< "2020-03");

// Get unique country codes
ccodes = unique(pre_data[., "cn"]);

// Get column labels 
{ cnames, keys } = getColLabels(pre_data, "Country");

// Get pre-covid summary table
table_name = spath2 $+ "precovid-summary-stats.csv";
pre_sum_table = getCABSummary(pre_data, "date3", "Country", "pct_ca_reg");
saved(pre_sum_table, table_name);
//output off;

// Get post-covid summary table
table_name = spath2 $+ "postcovid-summary-stats.csv";
post_sum_table = getCABSummary(post_data, "date3", "Country", "pct_ca_reg");
saved(post_sum_table, table_name);

//output off;

proc (1) = getCABSummary(reg_data, time_var, group_var, interest_var);
    local sum_table, fmt, mean_ca, min_ca, max_ca, sd_ca, tmp_data, start_date, 
    end_date, count_obs, country_names, lbls;    
    
    // Get variables and sort data
    reg_data = sortc(reg_data, group_var$|time_var);
    
    // Get statistics
    mean_ca = packr(asDF(aggregate(reg_data[., interest_var group_var], "mean", group_var), "country", "Mean CAB/GDP%"));
    min_ca =  packr(asDF(aggregate(reg_data[., interest_var group_var], "min", group_var), "country", "Min CAB/GDP%"));
    max_ca =  packr(asDF(aggregate(reg_data[., interest_var group_var], "max", group_var), "country", "Max CAB/GDP%"));
    sd_ca =  packr(asDF(aggregate(reg_data[., interest_var group_var], "sd", group_var), "country", "SD CAB/GDP%"));

    // Remove missing values to get accurate counts
    // and time ranges
    tmp_data = packr(reg_data[., interest_var time_var group_var]);
    start_date = asDF(aggregate(tmp_data[., time_var group_var], "min", group_var), "country", "Start Date");
    end_date = asDF(aggregate(tmp_data[., time_var group_var], "max", group_var), "country", "End Date");
    
    // Count observations by group
    count_obs = counts(tmp_data[., group_var], unique(tmp_data[., group_var])); 

    // Build summary table
    sum_table = mean_ca~sd_ca[., 2]~min_ca[., 2]~max_ca[., 2]~start_date[., 2]~end_date[., 2]~asDF(count_obs, "N");
    
//    fmt = "%15s%15.4f%15s%15d";
//    sprintf("%15s", getcolnames(sum_table)');
//    { country_names, lbls } = getColLabels(mean_ca[., 1], 1);
//    sprintf(fmt, country_names, mean_ca[., 2]~sd_ca[., 2]~min_ca[., 2]~max_ca[., 2], start_date[., 2]~end_date[., 2], asDF(count_obs, "N"));
//    sum_table;
    retp(sum_table);
endp;
