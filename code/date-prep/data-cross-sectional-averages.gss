/*
** This file computes and plots
** by year, the cross-sectional:
**       > Means
**       > Variances
*/
new;
library tspdlib;

/*
** Load data
*/
// Data loading path
fpath = "D:/development/GitHub/cab-persistency/data/";

// Data file
fname = "reg_data_group_dummy.dta";

// Results saving paths
spath = "D:/development/GitHub/cab-persistency/results/unit-root-tests/standard/";
spath2 = "D:/development/GitHub/cab-persistency/results/unit-root-tests/with-breaks/";

// Perform import
data = packr(loadd(fpath$+fname));
data = asdate(move(data), "%YQ%q", "date3");

/*
** Data transformations
*/
// Get unique country codes
ccodes = unique(data[., "cn"]);

// Take absolute value of CAB
data[., "pct_ca_reg"] = abs(data[., "pct_ca_reg"]);

// Filter data to include observations after 1960
data = delif(data, data[., "date3"] .<"1960-01-01");

/*
** Perform CSA and CSVAR
*/
// Mean by year 
csa_ca = aggregate(data[., "pct_ca_reg" "date3"], "mean", "date3");

// Variance by year
csvar_ca = aggregate(data[., "pct_ca_reg" "date2"], "variance", "date2");

/*
** Plot data
*/
// Plot control struct
struct plotControl ctl;
ctl = plotGetDefaults("xy");

// Set the first curve, left y-axis
// Set the second curve right y-axis
string which = { "left", "right" };
plotSetWhichYAxis(&ctl, which);

// Set the left y-axis range to between 0 and 10,
// and the right y-axis to between 0 and 100
plotSetYRange(&ctl, 0|10, 0|100);

// Set up the line style
plotSetLinePen(&ctl, 2, "black", 1|2);

// Set up legend
label = "Absolute Mean Value of Current Account (%GDP)"$|"Within Period Current Account (%GDP) Variance";
plotSetLegend(&ctl, label, "bottom center outside", 0);

// Create plots
plotXY(ctl, csa_ca[.,"date3"], csa_ca[.,"pct_ca_reg"]~csvar_ca[., "pct_ca_reg"]);

/*
** Compute mean bars
*/
{ mean_1, dts_1, t_1 } = plotGetPeriodCABMean(data, "pct_ca_reg", "date3", "", "1975");
{ mean_2, dts_2, t_2 } = plotGetPeriodCABMean(data, "pct_ca_reg", "date3", "1975", "1995");
{ mean_3, dts_3, t_3 } = plotGetPeriodCABMean(data, "pct_ca_reg", "date3", "1995", "2008-09");

// Plot control structure
struct plotControl plt;
plt = plotGetDefaults("xy");

// Set up the line style
plotSetLinePen(&plt, 2, "red", 3|3|3);

/*
** Declare 'myAnnotation' to be an instance of a plotAnnotation
** structure and fill it in with default values
*/
struct plotAnnotation myAnnotation;
myAnnotation = annotationGetDefaults();

annotationSetFont(&myAnnotation, "arial", 12, "black");

// Add mean lines
plotAddtshf(plt, dts_1, "years", ones(t_1,1)*mean_1);
text1 = "Pre-1975, Mean = " $+ ntos(mean_1, 3);
plotAddTextbox(myAnnotation, text1, asDate("1962"), mean_2);

plotAddtshf(plt, dts_2, "years", ones(t_2,1)*mean_2);
text2 = "1975-1994, Mean = " $+ ntos(mean_2, 3);
plotAddTextbox(myAnnotation, text2, asDate("1978"), mean_3+0.5);

plotAddtshf(plt, dts_3, "years", ones(t_3,1)*mean_3);
text2 = "1995-Sep.2008, Mean = " $+ ntos(mean_3, 3);
plotAddTextbox(myAnnotation, text2, asDate("1996"), 7);

proc (3) = plotGetPeriodCABMean(data, interest_var, time_var, date1, date2);
    local tmp_data, mean, dts, t;
    
    
    if date1 $== "";
        tmp_data = selif(data, data[., time_var] .< date2);
    elseif date2 $== "";
        tmp_data = selif(data, data[., time_var] .>= date1);
    else;
        tmp_data = selif(data, data[., time_var] .>= date1 .and data[., time_var] .<date2);
    endif;
    
    // Get period mean
    mean = meanc(tmp_data[., interest_var]);
    
    dts = unique(tmp_data[., time_var]);
    t = rows(dts);
    
    retp(mean, dts, t);
endp;


